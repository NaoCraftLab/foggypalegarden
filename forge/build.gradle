architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig "${modId}.mixins.json"
    }
}

configurations {
    common
    versionSpecificCommon

    shadowCommon
    shadowVersionSpecificCommon

    compileClasspath.extendsFrom(common, versionSpecificCommon)
    runtimeClasspath.extendsFrom(common, versionSpecificCommon)
    developmentForge.extendsFrom(common, versionSpecificCommon)
}

dependencies {
    forge "net.minecraftforge:forge:${forgeVersion}"

    modImplementation "me.shedaniel.cloth:cloth-config-forge:${commonClothConfigVersion}"

    common(project(path: ':core', configuration: 'namedElements')) { transitive false }
    shadowCommon(project(path: ':core', configuration: 'transformProductionForge')) { transitive false }

    versionSpecificCommon(project(path: ":command-$moduleCommand", configuration: 'namedElements')) { transitive false }
    shadowVersionSpecificCommon(project(path: ":command-$moduleCommand", configuration: 'transformProductionForge')) { transitive false }

    versionSpecificCommon(project(path: ":mixin-$moduleMixin", configuration: 'namedElements')) { transitive false }
    shadowVersionSpecificCommon(project(path: ":mixin-$moduleMixin", configuration: 'transformProductionForge')) { transitive false }
}

shadowJar {
    configurations = [project.configurations.shadowCommon, project.configurations.shadowVersionSpecificCommon]
    archiveClassifier.set('dev-shadow')
}

remapJar {
    inputFile = shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set('dev')
}

sourcesJar {
    def coreSources = project(':core').sourcesJar
    from coreSources.archiveFile.map { zipTree(it) }

    def commandSources = project(":command-$moduleCommand").sourcesJar
    from commandSources.archiveFile.map { zipTree(it) }

    def mixinSources = project(":mixin-$moduleMixin").sourcesJar
    from mixinSources.archiveFile.map { zipTree(it) }

    dependsOn coreSources, commandSources, mixinSources
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

configurations.configureEach {
    resolutionStrategy.force("net.sf.jopt-simple:jopt-simple:${forgeJoptSimpleVersion}")
}
